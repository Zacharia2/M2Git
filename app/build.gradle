apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'ts.realms.m2git'
    defaultConfig {
        applicationId "ts.realms.m2git"
        compileSdk 34
        //noinspection ExpiredTargetSdkVersion
        targetSdk 31
        minSdk 31
        versionCode 261
        versionName "1.8.0"

        // compileSdk 应始终使用最新版本，但不会影响运行时行为。
        // targetSdk 决定了应用是否启用新系统的行为变化，更新时需充分测试。
        // minSdk 决定了应用的最低支持版本，设置过低可能增加开发和测试成本。
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    lint {
        abortOnError false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
    }

    packagingOptions {
        resources {
            excludes += ['**/plugin.properties']
            excludes += "META-INF/DEPENDENCIES"
            excludes += "META-INF/LICENSE*"
            excludes += "META-INF/NOTICE*"
            excludes += "META-INF/MANIFEST.MF"
        }
    }
}

configurations.configureEach {
    //  replace com.jcraft.jsch to com.github.mwiede:jsch
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "com.jcraft" && details.requested.name == "jsch") {
            details.useTarget("com.github.mwiede:jsch:0.2.11")
        }
    }
    exclude module: 'httpclient'
    exclude module: "org.eclipse.jgit"
}

//tasks.withType(JavaCompile).configureEach {
//    options.compilerArgs << "-Xlint:deprecation"
//}

dependencies {
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.annotation:annotation:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.12.0'

    // Jgit
    //implementation 'org.eclipse.jgit:org.eclipse.jgit:6.10.1.202505221210-r'
    implementation files('libs/org.eclipse.jgit-6.10.1.202505221210-r.jar')
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:6.10.1.202505221210-r'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.lfs:6.10.1.202505221210-r'

    // mws
    implementation("io.milton:milton-server-ent:4.0.5.2500") {
        exclude group: "org.mnode.ical4j", module: "ical4j"
        exclude group: "org.apache.oltu.oauth2", module: "org.apache.oltu.oauth2.common"
        exclude group: "org.eclipse.angus", module: "jakarta.mail"
        exclude group: "com.sun.activation", module: "jakarta.activation"
        exclude group: "com.sun.mail", module: "jakarta.mail"  // 新增：排除重复的 mail 实现
    }

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.7.0"
    implementation "androidx.lifecycle:lifecycle-livedata:2.7.0"
    implementation "androidx.lifecycle:lifecycle-runtime:2.7.0"

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.mwiede:jsch:0.2.11'  // max
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

    //  Application Crash Reports
    implementation 'ch.acra:acra-mail:5.9.7'    // max
    implementation 'ch.acra:acra-dialog:5.9.7'  // max

    //  Debug and Test
    debugImplementation 'com.facebook.stetho:stetho:1.6.0'
    debugImplementation 'com.facebook.stetho:stetho-timber:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.13'
}
